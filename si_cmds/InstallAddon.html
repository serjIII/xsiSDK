<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<!-- saved from url=(0024)http://docs.autodesk.com -->
<html>
   <head>
<link href="../style/prettify.css" type="text/css" rel="stylesheet" />
<script type="text/javascript" src="../scripts/prettify.js"></script><script src="../scripts/lib/jquery-1.9.1.min.js" type="text/javascript"></script><meta http-equiv="Content-Type" content="text/html; charset=UTF-8" /><script type="text/javascript" src="../scripts/utils/adsk.redirect.js"></script>
      <title>InstallAddon</title>
   
</head>
   <body height="100%"><div class="body_content" id="body-content"><style>
/*  These settings override ones in the default stylesheets
	used by the merged files that cause problems in combination
	with the standard HTML help styles and page templates. */

div, div.example, div.tip {
	 margin:0;
}

td.example {
	background: inherit;
}

/* This fixes a problem with nested pre blocks on an example page */
pre pre {
	display:block;
	padding:0;
	border-style:none;
}
</style><link rel="stylesheet" href="si_cmds/css/en.css" type="text/css" /><script>$(document).ready(function() { yepnope.injectJs("./si_cmds/lib/utils.js"); });</script><script>$(document).ready(function() { yepnope.injectJs("./scripts/ac_common.js"); });</script><script type="text/javascript">var reflinkid = "si_cpp"; var reflinkdata = new Array(); function closeRefLinkDiv() { }; $(document).on('click', function() { closeRefLinkDiv(); } );</script><script>$(document).ready(function() { yepnope.injectJs("./scripts/multireflink.js"); });</script><script>$(document).ready(function () { prettyPrint(); } );</script><script>$("div#WidgetFloaterPanels,link[href*='microsofttranslator.com'],script[src*='microsofttranslator.com'],script[src*='bing.com']").remove();</script><script type='text/javascript'>$("div#navigation,div#breadcrumbs,div#banner").attr("translate","no"); var mtLocation = ((location && location.href && location.href.indexOf('https') == 0)?'https://ssl.microsofttranslator.com':'http://www.microsofttranslator.com')+'/ajax/v3/WidgetV3.ashx?siteData=y5CYlxTRD0znCzRLDwX0Wy7-g1EdC1XA4dSC-Y1LtaeScyli8_Ps5jPKqTr4xKxMI0OOUfkDplvX3uxN0JnPclebSYW8_J1HBzf4VLQEzQ8M4PsYXF_cMyp1Oumaetky&category=5297189e-446b-459e-ae1d-9d0360400781_tech&ctf=True&ui=true&settings=Manual&from=en&hidelanguages='; yepnope.injectJs(mtLocation, function() {}, { charset:'utf-8', type:'text/javascript' } );</script><script type="text/javascript">
 </script><!-- begin MT -->
            
            <div id='MicrosoftTranslatorWidget' class='Dark' style='position:absolute;right:20px;top:5px;z-index:100;color:white;background-color:#555555;height:58px;overflow:hidden'></div><div id="reflinkdiv" style="display:none; font-family: 'RobotoRegular', Arial, Tahoma, sans-serif; visibility:hidden; position:absolute; z-index:40001; padding:5px; border-style:solid; border-width:1px; border-color:#666666; background-color:#dfdfdf; overflow:visible"></div>
      <div>
         <div class="head">
            <h1>InstallAddon</h1>
         </div>

<h2 class="pagehead">InstallAddon</h2>

<h3>Description</h3>
<div><p>
Installs an add-on file in Softimage. To execute this command, you need to have an add-on file saved on disk. Add-on files 
for Softimage have the .xsiaddon extension. Add-on files contain settings for the layout of Softimage, toolbars, keymaps, custom 
commands, etc.<br /><br />
This command is the scripting equivalent of launching the Install option from the File > Addon location in the main menu.
</p></div>

<h3>Scripting Syntax</h3>
<div><p><table>
	<tr>
		<td class="example"><pre>InstallAddon( FileName, [InstallPath] );</pre>		</td>	</tr>
</table>
</p></div>

<h3>Parameters</h3>
<div><p><table cellpadding="5" cellspacing="5">
	<tr>
		<th title="Name of the parameter">Parameter		</th>
		<th title="Data type">Type		</th>
		<th title="Description of the parameter">Description		</th>
	</tr>
	<tr>
		<td class="name">FileName		</td>
		<td><a href="#!/url=./files/String.htm">String</a>		</td>
		<td>
 The .xsiaddon file. 		</td>
	</tr>
	<tr>
		<td class="name">InstallPath		</td>
		<td><a href="#!/url=./si_om/siInstallationPath.html">siInstallationPath</a>		</td>
		<td>

		Use this to specify a installation location for the addon. siUserAddonPath and siWorkgroupAddonPath 
		are the recommended values. If siUnknownPath is specified then the add-on will be installed at the 
		default location specified inside the add-on file (see <a href="#!/url=./si_om/Addon.DefaultInstallationPath.html">Addon.DefaultInstallationPath</a>).  
				<p class="cell"><span class="hilite">Default Value: </span>
7 (siUnknownPath)			</p>
		</td>
	</tr>
</table>
</p></div>

<h3>Examples</h3>
<div><p><h4>VBScript Example</h4><div class="example"><table>
<tr>
<td class="example"><pre class="vbscript">'---------------------------------------------------------
' This script demonstrates how to install and uninstall 
' user add-ons and factory add-ons.
'---------------------------------------------------------
' Get the filename of an available add-on file, and install it, in the user add-on path.
myAddonFile = createAddonForDemo( siUserPath )
Application.LogMessage "Installing: " &amp; myAddonFile
InstallAddon myAddonFile, siUserAddonPath
' Now find the installed file.
myAddonFile = XSIUtils.BuildPath( Application.InstallationPath(siUserAddonPath), "DemoAddon.xsiaddon" )
Application.LogMessage "Uninstalling: " &amp; myAddonFile
UnInstallAddon myAddonFile
killCmdForDemo		' clean up the command that was loaded from the add-on
' Now re-install it to the AddonPath.
myAddonFile = createAddonForDemo( siFactoryPath )
Application.LogMessage "Installing: " &amp; myAddonFile
InstallAddon myAddonFile, siAddonPath
' Now uninstall the add-on (again).
myAddonFile = XSIUtils.BuildPath( Application.InstallationPath(siAddonPath), "DemoAddon.xsiaddon" )
Application.LogMessage "Uninstalling: " &amp; myAddonFile
UnInstallAddon myAddonFile
killCmdForDemo		' the plug-in is not automatically unloaded when the add-on is uninstalled
'---------------------------------------------------------
' Output from this script:
'INFO : "Installing: &lt;UserPath&gt;\Addons\DemoAddon.xsiaddon"
'INFO : "Uninstalling: &lt;UserPath&gt;\Addons\DemoAddon.xsiaddon"
'INFO : "Installing: &lt;FactoryPath&gt;\Addons\DemoAddon.xsiaddon"
'INFO : "Uninstalling: &lt;FactoryPath&gt;\Addons\InstalledAddons\DemoAddon.xsiaddon"
'---------------------------------------------------------
' 	*** Helper functions *** 
'
' Create an add-on package on the fly
function createAddonForDemo( in_Where )
	dim l_AddonFile, l_PluginFile, l_AddonObj
	l_AddonFile = GetAddonFileLocation( in_Where )
	l_PluginFile = createCmdForDemo()
	Set l_AddonObj = Application.CreateAddon()
	l_AddonObj.AddItem siPluginAddonItemType, l_PluginFile
	l_AddonObj.Save l_AddonFile
	' Now that it's in the .xsiaddon file, kill the source
	killCmdForDemo 
	createAddonForDemo = l_AddonFile
end function
' Validate the add-on file location
function GetAddonFileLocation( in_Where )
	dim l_folderLocation, l_fso
	If in_Where = siUserPath Then
		l_folderLocation = Application.InstallationPath( siUserAddonPath )
	Else
		l_folderLocation = Application.InstallationPath( siAddonPath )
	End If
	Set l_fso = CreateObject( "Scripting.FileSystemObject" )
	' Make sure the Addons folder exists 
	If Not l_fso.FolderExists(l_folderLocation) Then 
		l_fso.CreateFolder l_folderLocation
	End If
	GetAddonFileLocation = XSIUtils.BuildPath( l_folderLocation, "DemoAddon.xsiaddon" )
end function
' Create the self-installing command plug-in on disk and load it
function createCmdForDemo()
	dim l_fileLocation, l_fso, l_ts
	l_fileLocation = XSIUtils.BuildPath( Application.InstallationPath(siUserPath), _
		"Application", "Plugins", "VBSCmdForAddonsDemoPlugin.vbs" )
	Set l_fso = CreateObject( "Scripting.FileSystemObject" )
	Set l_ts = l_fso.CreateTextFile( l_fileLocation )
	l_ts.Write writeCmdImpl()
	l_ts.Close
	Application.LoadPlugin l_fileLocation
	createCmdForDemo = l_fileLocation
end function
' Supply the implementation of the self-installing command plug-in
function writeCmdImpl()
	writeCmdImpl = "function XSILoadPlugin( in_reg )" &amp; vbCrLf _
	&amp; vbTab &amp; "in_reg.Author = " &amp; Chr(34) &amp; "InstallAddonDemo" &amp; Chr(34) &amp; vbCrLf _
	&amp; vbTab &amp; "in_reg.Name = " &amp; Chr(34) &amp; "VBSCmdForAddonsDemoPlugin" &amp; Chr(34) &amp; vbCrLf _
	&amp; vbTab &amp; "in_reg.Major = 1" &amp; vbCrLf _
	&amp; vbTab &amp; "in_reg.Minor = 0" &amp; vbCrLf _
	&amp; "" &amp; vbCrLf _
	&amp; vbTab &amp; "in_reg.RegisterCommand " &amp; Chr(34) &amp; "VBSCmdForAddonsDemo" &amp; Chr(34) &amp; ", " &amp; Chr(34) &amp; "VBSCmdForAddonsDemo" &amp; Chr(34) &amp; vbCrLf _
	&amp; vbTab &amp; "XSILoadPlugin = True" &amp; vbCrLf _
	&amp; "end function" &amp; vbCrLf _
	&amp; "" &amp; vbCrLf _
	&amp; "function VBSCmdForAddonsDemo_Init( in_ctxt )" &amp; vbCrLf _
	&amp; vbTab &amp; "Set oCmd = in_ctxt.Source" &amp; vbCrLf _
	&amp; vbTab &amp; "oCmd.Description = " &amp; Chr(34) &amp; "'Hello World' command for demonstration" &amp; Chr(34) &amp; vbCrLf _
	&amp; vbTab &amp; "oCmd.ReturnValue = False" &amp; vbCrLf _
	&amp; vbTab &amp; "VBSCmdForAddonsDemo_Init = True" &amp; vbCrLf _
	&amp; "end function" &amp; vbCrLf _
	&amp; "" &amp; vbCrLf _
	&amp; "function VBSCmdForAddonsDemo_Execute(  )" &amp; vbCrLf _
	&amp; vbTab &amp; "Application.LogMessage " &amp; Chr(34) &amp; "Hello, World!" &amp; Chr(34) &amp; ", siInfo" &amp; vbCrLf _
	&amp; vbTab &amp; "VBSCmdForAddonsDemo_Execute = True" &amp; vbCrLf _
	&amp; "end function"	
end function
' Remove the self-installing command plug-in (and delete from disk)
sub killCmdForDemo()
	For Each p In Application.Plugins
		If p.Name = "VBSCmdForAddonsDemoPlugin" Then
			Application.UnloadPlugin p.Filename, true
			Exit For
		End If
	Next
end sub</pre></td></tr>
</table>
</div></p></div>

<h3>See Also</h3>
<div><p><a href="#!/url=./si_cmds/UnInstallAddon.html">UnInstallAddon</a> <a href="#!/url=./si_om/XSIApplication.InstallAddon.html">XSIApplication.InstallAddon</a> <a href="#!/url=./si_om/XSIApplication.UnInstallAddon.html">XSIApplication.UnInstallAddon</a> <a href="#!/url=./si_om/Addon.html">Addon</a> <a href="#!/url=./si_om/Plugin.html">Plugin</a></p></div>

      <div class="footer-block"><a href="../html/ac.cmtdialog.htm" class="comments-anchor" target="_blank"><span class="comments-link">Please send us your comment about this page</span></a></div><br /></div>
   </div></body>
</html>
